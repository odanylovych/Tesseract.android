plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

def DEFAULT_COMPILE_SDK_VERSION = 32
def DEFAULT_MIN_SDK_VERSION = 21
def DEFAULT_TARGET_SDK_VERSION = 32
def DEFAULT_NDK_VERSION = '24.0.8215888'
def DEFAULT_RUST_NDK_TARGETS = ["arm", "x86", "arm64", "x86_64"]

def getBuildProperty(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdk getBuildProperty('compileSdkVersion', DEFAULT_COMPILE_SDK_VERSION)
    ndkVersion getBuildProperty('ndkVersion', DEFAULT_NDK_VERSION)

    defaultConfig {
        applicationId "one.tesseract.example.wallet"
        minSdk getBuildProperty('minSdkVersion', DEFAULT_MIN_SDK_VERSION)
        targetSdk getBuildProperty('targetSdkVersion', DEFAULT_TARGET_SDK_VERSION)
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        viewBinding true
    }
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

tasks.whenTaskAdded { task ->
    if ((task.name == 'javaPreCompileDebug' || task.name == 'javaPreCompileRelease')) {
        task.dependsOn 'cargoBuild'
    }
}

cargo {
    module  = "../wallet_native"       // Or whatever directory contains your Cargo.toml
    libname = "wallet"          // Or whatever matches Cargo.toml's [package] name.
    targets = getBuildProperty('rustNdkTargets', DEFAULT_RUST_NDK_TARGETS)  // See bellow for a longer list of options
    targetDirectory = '../../target'
}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.2'

    implementation project(':java::interop-rust')
    implementation project(':java:tesseract-ipc')
    implementation project(':java:tesseract-ipc-service')

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}